plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72' apply false
    id 'org.jetbrains.kotlin.plugin.spring' version '1.3.72' apply false
    id 'com.github.ben-manes.versions' version '0.29.0'
    id 'org.springframework.boot' version '2.3.2.RELEASE' apply false
    id 'info.solidsoft.pitest' version '1.5.1' apply false
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'kotlin'
    apply plugin: 'org.jetbrains.kotlin.plugin.allopen'
    apply plugin: 'jacoco'
    apply plugin: 'info.solidsoft.pitest'

    group = 'com.example'

    repositories {
        jcenter()
    }

    dependencies {
        implementation('org.jetbrains.kotlin:kotlin-stdlib-jdk8')
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage'
        }
//        pitest('eu.stamp-project:descartes:1.2.6') // comment out for default mutation engine - alters behaviour
    }

    test {
        useJUnitPlatform()
        systemProperty 'spring.test.constructor.autowire.mode', 'all'
    }

    pitest {
        pitestVersion = '1.5.2'
        junit5PluginVersion = '0.12'
//        avoidCallsTo = ["kotlin.jvm.internal", "org.slf4j"]
//        verbose = true
        jvmArgs = ['-Dspring.test.constructor.autowire.mode=all']

        // default mutation engine only
        mutators = ["STRONGER"]

        // Descartes only
//        mutationEngine = 'descartes'
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            freeCompilerArgs = ['-Xjsr305=strict']
            jvmTarget = '11'
        }
    }

    allOpen {
        annotation("org.springframework.web.bind.annotation.RestController")
        annotation("org.springframework.context.annotation.Configuration")
    }
}
